# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer : James Kittsmiller (AJSlye) <james@nulogicsystems.com>

pkgbase=qtcurve
pkgname=('qtcurve-utils-next' 'qtcurve-gtk2-next' 'qtcurve-qt4-next' 'qtcurve-qt5-next')
pkgver=1.8.18.294.gec221dd9
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('i686' 'x86_64')
url='https://github.com/QtCurve/qtcurve'
license=('LGPL')
groups=('qtcurve')
makedepends=('git' 'extra-cmake-modules' 'gtk2' 'qt4' 'qt5-svg' 'qt5-x11extras' 'frameworkintegration' 'kdelibs4support' 'kdesignerplugin' 'kdoctools')
source=("git://anongit.kde.org/qtcurve.git"
                "normal_shadows.patch")
md5sums=('SKIP'
                     '9e85cb6c5583715b0d0e2c1d93e14ddb')

pkgver() {
  cd $pkgbase

  git describe | sed -e 's/-/./g'
}

prepare() {
  cd $pkgbase

  patch -Np1 -i ../normal_shadows.patch
}

build() {
  cd $pkgbase

  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT4_ENABLE_KDE=false
  make
}

package_qtcurve-utils-next() {
  depends=('libx11' 'gcc-libs')
  conflicts=('qtcurve-utils')
  provides=('qtcurve-utils')
  cd $pkgbase/build/lib/utils
  make DESTDIR="$pkgdir" install
}

package_qtcurve-gtk2-next() {
  depends=('qtcurve-utils-next' 'gtk2')
  conflicts=('qtcurve-gtk2')
  provides=('qtcurve-gtk2')
  cd $pkgbase/build/gtk2
  make DESTDIR="$pkgdir" install
  cd ../lib/cairo
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt4-next() {
  depends=('qtcurve-utils-next' 'qt4')
  conflicts=('qtcurve-qt4')
  provides=('qtcurve-qt4')
  cd $pkgbase/build/qt4
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt5-next() {
  depends=('qtcurve-utils-next' 'qt5-svg' 'qt5-x11extras')
  conflicts=('qtcurve-qt5')
  provides=('qtcurve-qt5')
  cd $pkgbase/build/qt5
  make DESTDIR="$pkgdir" install
}
